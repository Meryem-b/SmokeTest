apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter() // needed for corgette-jvm
    mavenCentral() // TODO: check if all deps are avail on jcenter then get rid of this
}

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

task('cucumber-feature') {
    description 'Runs a single feature test (specified using -Pfeature=<the .feature file>'
    dependsOn assemble, compileTestJava
    doLast {
        if (!project.hasProperty('feature')) {
            throw new GradleException('feature parameter must be defined')
        }
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.test.output + sourceSets.test.resources.sourceDirectories
            args = ['--plugin', 'pretty', '--glue', 'uk.gov.dvsa.mot', 'features/' + feature]
        }
    }
}

task('cucumber-tag') {
    description 'Runs all features matching the tag(s) (specified using -Ptag=<tag(s)>'
    dependsOn assemble, compileTestJava
    doLast {
        if (!project.hasProperty('tag')) {
            throw new GradleException('tag parameter must be defined')
        }
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.test.output + sourceSets.test.resources.sourceDirectories
            args = ['--plugin', 'pretty', '--glue', 'uk.gov.dvsa.mot', '--tags', tag, 'features']
        }
    }
}

task('cucumber-all') {
    description 'Runs all feature tests in serial'
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.test.output + sourceSets.test.resources.sourceDirectories
            args = ['--plugin', 'pretty', '--glue', 'uk.gov.dvsa.mot', 'features']
        }
    }
}

test {
    description 'Runs all scenarios as JUnit tests in parallel'
    include 'uk/gov/dvsa/mot/junit/CourgetteRunner.class'
    testLogging.showStandardStreams = true
    outputs.upToDateWhen { false }
}

dependencies {
    testCompile 'info.cukes:cucumber-java8:1.2.5'
    testCompile 'info.cukes:cucumber-guice:1.2.5'
    testCompile 'info.cukes:cucumber-junit:1.2.5'
    testCompile 'io.github.prashant-ramcharan:courgette-jvm:1.3.0'
    testCompile 'org.seleniumhq.selenium:selenium-java:3.4.0'
    testCompile 'org.slf4j:slf4j-api:1.7.25'
    testCompile 'org.slf4j:log4j-over-slf4j:1.7.5'
    testCompile 'ch.qos.logback:logback-classic:1.2.3'
    testCompile 'com.google.inject:guice:4.1.0'
    testCompile 'org.apache.commons:commons-configuration2:2.1.1'

    // depends upon the "mot/2fa-pin-generator" project being checked out alongside this project, then built manually
    testCompile files('../2fa-pin-generator/build/libs/2fa-generator-1.0.jar')
}

