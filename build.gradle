apply plugin: 'java'

ext {
    javaVersion = '1.8'
    cucumberVersion = '1.2.5'
    seleniumVersion = '3.4.0'
    springVersion = '4.3.8.RELEASE'
    slf4jVersion = '1.7.5'
}

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

repositories {
    jcenter()
}

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

task('cucumber-feature') {
    description 'Runs a single feature test (specified using -Pfeature=<the .feature file>'
    dependsOn assemble, compileTestJava
    doLast {
        if (!project.hasProperty('feature')) {
            throw new GradleException('feature parameter must be defined')
        }
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'uk.gov.dvsa.mot', 'features/' + feature]
        }
    }
}

task('cucumber-tag') {
    description 'Runs all features matching the tag(s) (specified using -Ptag=<tag(s)>'
    dependsOn assemble, compileTestJava
    doLast {
        if (!project.hasProperty('tag')) {
            throw new GradleException('tag parameter must be defined')
        }
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'uk.gov.dvsa.mot', '--tags', tag, 'features']
        }
    }
}

task('cucumber-all') {
    description 'Runs all feature tests in serial'
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'uk.gov.dvsa.mot', 'features']
        }
    }
}

test {
    description 'Runs all scenarios as JUnit tests in parallel'
    include 'uk/gov/dvsa/mot/junit/CourgetteRunner.class'
    outputs.upToDateWhen { false }
}

dependencies {
    testCompile "info.cukes:cucumber-java8:$cucumberVersion"
    testCompile "info.cukes:cucumber-spring:$cucumberVersion"
    testCompile "info.cukes:cucumber-junit:$cucumberVersion"
    testCompile "io.github.prashant-ramcharan:courgette-jvm:1.3.0"
    testCompile "org.seleniumhq.selenium:selenium-java:$seleniumVersion"
    testCompile "org.slf4j:slf4j-api:$slf4jVersion"
    testCompile "org.slf4j:log4j-over-slf4j:$slf4jVersion"
    testCompile "ch.qos.logback:logback-classic:1.2.3"
    testCompile "org.springframework:spring-context:$springVersion"
    testCompile "org.springframework:spring-test:$springVersion"
    testCompile "javax.inject:javax.inject:1"

    // depends upon the "mot/2fa-pin-generator" project being checked out alongside this project, then built manually
    testCompile files("../2fa-pin-generator/build/libs/2fa-generator-1.0.jar")
}

